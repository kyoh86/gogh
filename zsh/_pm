#compdef gogh

function _pm () {
    local context curcontext=$curcontext state line
    declare -A opt_args
    local ret=1

    _arguments -C \
        '(-h --help)'{-h,--help}'[show help]' \
        '(-v --version)'{-v,--version}'[print the version]' \
        '1: :__pm_commands' \
        '*:: :->args' \
        && ret=0

    case $state in
        (args)
            case $words[1] in
                (get)
                    _arguments -C \
                        '(-u --update)'{-u,--update}'[Update local repository if cloned already]' \
                        '(-)*:: :->null_state' \
                        && ret=0
                    ;;
                (list)
                    _arguments -C \
                        '(-e --exact)'{-e,--exact}'[Perform an exact match]' \
                        '(-p --full-path)'{-p,--full-path}'[Print full paths]' \
                        '--unique[Print unique subpaths]' \
                        '(-)*:: :->null_state' \
                        && ret=0
                    ;;
                (find)
                    _arguments -C \
                        '1: :__pm_repositories' \
                        && ret=0
                    ;;
                (help|h)
                    __pm_commands && ret=0
                    ;;
            esac
            ;;
    esac

    return ret
}

__pm_repositories () {
    local -a _repos
    _repos=( ${(@f)"$(_call_program repositories gogh list --unique)"} )
    _describe -t repositories Repositories _repos
}

__pm_commands () {
    local -a _c
    _c=(
        'get:Clone/sync with a remote repository'
        'list:List local repositories'
        'find:Look into a local repository'
        "root:Show repositories' root"
        'help:Show a list of commands or help for one command'
    )

    _describe -t commands Commands _c
}

_pm "$@"

